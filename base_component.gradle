//模块名称
def componentName = project.getName().replaceAll("component-", "")
def isRunAlone = !App.components.contains(componentName)
if (isRunAlone) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'com.alibaba.arouter'

android {
    compileSdkVersion App.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion App.minSdkVersion
        targetSdkVersion App.targetSdkVersion
        if (isRunAlone) {
            applicationId App.organization + "." + componentName
            multiDexEnabled true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ AROUTER_MODULE_NAME : project.getName() ]
            }
        }
        versionCode 1
        versionName "1.0.0"
        resourcePrefix componentName + "_"
        resValue "string", componentName + "_module_name", project.getName()
    }

    if (isRunAlone) {
        sourceSets {
            main {
                manifest.srcFile 'src/main/runalone/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/runalone/java']
                res.srcDirs = ['src/main/res', 'src/main/runalone/res']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    dependencies {
        implementation project(':common')
    }
}